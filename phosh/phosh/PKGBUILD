# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.49.0
pkgrel=2
pkgdesc="Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL-3.0-or-later')
depends=(
    'callaudiod'
    'evince'
    'evolution-data-server'
    'feedbackd'
    'gcr'
    'gtk3'
    'libadwaita'
    'libcap'
    'libgmobile'
    'libhandy'
    'libnm'
    'libpulse'
    'gnome-bluetooth-3.0'
    'gnome-desktop'
    'gnome-session'
    'gnome-shell'
    'phosh-osk-stevia'
    'upower'
)
makedepends=(
    'gi-docgen'
    'glib2-devel'
    'gobject-introspection'
    'meson'
    'python-docutils'
    'python-packaging'
    'wayland-protocols'
)
checkdepends=('xmlstarlet' 'xorg-server-xvfb')
install="$pkgname.install"
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
        'pam_phosh'
        'phosh.service'
        'https://gitlab.gnome.org/World/Phosh/phosh/-/merge_requests/1763.patch')
b2sums=('03df719cbcb95a1378b8f45c8a21dffcc188e4dfb43147b57e90edfe9be1321db56ae3dca2071365b4227b549bdddceecdeddaf3fe26d6168c82f0398ade9d19'
        'SKIP'
        'd89e16605e2e8e29834f9537bd09ef3f76f1cf6420cdd99346a760cab8c7217f86898be66c24b206b464239fc8b5277150abd19bae81af72187a435bd8c2f869'
        '5a769e4bda9c2c23cc386ac657dfb4267f130ee22644c3bf247f7373ee53afcf30d3086906e020d005e0b860742356f004cc5543bd1ed62fb82fb71d7e9b430e'
        '5829bfbb74df9b3791ef8a3209ca3a444eabd32b9633bf0c4f2b3d41584e4bc30b8348a606277b23ef229123e27bc6a60d63080936d869f074cdef20c026f242')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i "$srcdir/1763.patch"
}

build() {
    local meson_options=(
        --libexecdir=/usr/lib/$pkgname
        -D gtk_doc=true
        -D man=true
        -D phoc_tests=disabled
        -D bindings-lib=true
    )
    arch-meson $pkgname-$pkgver output "${meson_options[@]}"
    meson compile -C output
}

check() {
    xvfb-run meson test -C output --print-errorlogs
}

package() {
    meson install -C output --destdir "$pkgdir"

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
}
