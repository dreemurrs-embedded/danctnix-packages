# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.53.0
pkgrel=1
pkgdesc="Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL-3.0-or-later')
depends=(
    'callaudiod'
    'evince'
    'evolution-data-server'
    'feedbackd'
    'gcr'
    'gtk3'
    'libadwaita'
    'libcap'
    'libgmobile'
    'libhandy'
    'libnm'
    'libpulse'
    'gnome-bluetooth-3.0'
    'gnome-desktop'
    'gnome-session'
    'gnome-shell'
    'qrcodegen-cmake'
    'stevia'
    'upower'
)
makedepends=(
    'gi-docgen'
    'glib2-devel'
    'gobject-introspection'
    'meson'
    'python-docutils'
    'python-packaging'
    'wayland-protocols'
)
checkdepends=('xmlstarlet' 'xorg-server-xvfb')
install="$pkgname.install"
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
        'pam_phosh'
        'phosh.service')
b2sums=('3ff2cb6078a6c32d0dff55f61b66d9c619abf94cd38951efd722de3fe10269467910ecbb8a8599778de37e07632aa2ffaad06911af7d2a47ac9896ab1848d23e'
        'SKIP'
        'd89e16605e2e8e29834f9537bd09ef3f76f1cf6420cdd99346a760cab8c7217f86898be66c24b206b464239fc8b5277150abd19bae81af72187a435bd8c2f869'
        '5a769e4bda9c2c23cc386ac657dfb4267f130ee22644c3bf247f7373ee53afcf30d3086906e020d005e0b860742356f004cc5543bd1ed62fb82fb71d7e9b430e')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
    local meson_options=(
        --libexecdir=/usr/lib/$pkgname
        -D gtk_doc=true
        -D man=true
        -D phoc_tests=disabled
        -D bindings-lib=true
    )
    arch-meson $pkgname-$pkgver output "${meson_options[@]}"
    meson compile -C output
}

check() {
    xvfb-run meson test -C output --print-errorlogs
}

package() {
    meson install -C output --destdir "$pkgdir"

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
}
