# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-osk-stevia
pkgver=0.52.0
pkgrel=1
pkgdesc="On-screen keyboard for Phosh"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
  'feedbackd'
  'gnome-desktop'
  'gtk3'
  'hunspell'
  'json-glib'
  'libgmobile'
  'libhandy'
  'wayland'
)
makedepends=(
  'gi-docgen'
  'glib2-devel'
  'glslang'
  'gobject-introspection'
  'meson'
  'python-docutils'
  'wayland-protocols'
)
checkdepends=('xorg-server-xvfb')
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('6841eb57b5365e2303b1645bdf37918485df05796e7ced9bf6992b68e3c886adb6af491b2e7c84398d2283533c3d640e86553fc37e73bed0efc7d7fea69967e2'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  local meson_options=(
    -D gtk_doc=true
    -D man=true
  )

  arch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
