# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-osk-stevia
pkgver=0.51.0
pkgrel=1
pkgdesc="On-screen keyboard for Phosh"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
  'feedbackd'
  'gnome-desktop'
  'gtk3'
  'hunspell'
  'json-glib'
  'libgmobile'
  'libhandy'
  'wayland'
)
makedepends=(
  'gi-docgen'
  'glib2-devel'
  'glslang'
  'gobject-introspection'
  'meson'
  'python-docutils'
  'wayland-protocols'
)
checkdepends=('xorg-server-xvfb')
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('81a84606524a9cf1cc9147aa771aa83b9ae8a684c37ec086fd65e8aa0fcb9532d811f812c1d25f9366fc6ff4e319d78f361187214f4f8cc0d7b3bb2d3b539794'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  local meson_options=(
    -D gtk_doc=true
    -D man=true
  )

  arch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  LC_ALL=C.UTF-8 xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
