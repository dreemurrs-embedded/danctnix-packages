# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc
pkgver=0.51.0
pkgrel=1
pkgdesc="wlroots based Wayland compositor for mobile phones"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('LGPL-2.1-or-later AND GPL-3.0-or-later AND MIT AND (GPL-3.0-or-later or MIT)')
depends=(
  'gobject-introspection'
  'gnome-desktop'
  'libglvnd'
  'libgmobile'
  'libinput'
  'libliftoff'
  'libxcb'
  'mutter'
  'seatd'
  'xcb-util-errors'
  'xcb-util-renderutil'
  'xcb-util-wm'
  'wayland-protocols'
)
makedepends=(
  'gi-docgen'
  'glib2-devel'
  'glslang'
  'meson'
  'python-docutils'
  'vulkan-headers'
)
checkdepends=('xorg-server-xvfb')
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('74f5ca6877cc9d88328e9284801b9eb0323b9f1ad74381e1bb6580a863bcdabbc9520b4a19076672bed55b83227870fdf737e3c3939e53d333e82c323b7d3e90'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

prepare() {
  cd $pkgname-$pkgver/subprojects/wlroots-0.19.x
  patch -p1 -i "../packagefiles/wlroots/0001-Revert-layer-shell-error-on-0-dimension-without-anch.patch"
}

build() {
  local meson_options=(
    --wrap-mode default
    -D embed-wlroots=enabled
    -D gtk_doc=true
    -D man=true
    --default-library=static
  )

  arch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Remove unnecessary files
  rm -r "$pkgdir"/usr/lib
  rm -r "$pkgdir"/usr/include
}
