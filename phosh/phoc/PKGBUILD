# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc
pkgver=0.50.0
pkgrel=2
pkgdesc="wlroots based Wayland compositor for mobile phones"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('LGPL-2.1-or-later AND GPL-3.0-or-later AND MIT AND (GPL-3.0-or-later or MIT)')
depends=(
  'gobject-introspection'
  'gnome-desktop'
  'libglvnd'
  'libgmobile'
  'libinput'
  'libliftoff'
  'libxcb'
  'mutter'
  'seatd'
  'xcb-util-errors'
  'xcb-util-renderutil'
  'xcb-util-wm'
  'wayland-protocols'
)
makedepends=(
  'gi-docgen'
  'glib2-devel'
  'glslang'
  'meson'
  'python-docutils'
  'vulkan-headers'
)
checkdepends=('xorg-server-xvfb')
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('03f49c43512b6d3a36aa813fa766318136d6fc6889886fb005b6725b6bf4f46445907feb066caf6a83f54d01270cdd115538f759d8c13e107ddf574c0784fa63'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

prepare() {
  cd $pkgname-$pkgver/subprojects/wlroots-0.19.x
  patch -p1 -i "../packagefiles/wlroots/0001-Revert-layer-shell-error-on-0-dimension-without-anch.patch"
}

build() {
  local meson_options=(
    --wrap-mode default
    -D embed-wlroots=enabled
    -D gtk_doc=true
    -D man=true
    --default-library=static
  )

  arch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  WLR_RENDERER=pixman xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Remove unnecessary files
  rm -r "$pkgdir"/usr/lib
  rm -r "$pkgdir"/usr/include
}
