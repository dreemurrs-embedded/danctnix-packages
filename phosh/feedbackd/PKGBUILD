# Maintainer: Danct12 <danct12@disroot.org>
pkgname=feedbackd
pkgver=0.8.8
pkgrel=1
pkgdesc="A daemon to provide haptic, led and audio feedback triggered by application events"
url="https://gitlab.freedesktop.org/agx/feedbackd"
arch=('x86_64' 'armv7h' 'aarch64')
install="$pkgname.install"
license=('GPL-3.0-or-later AND LGPL-2.1-or-later')
depends=(
  'gsound'
  'json-glib'
  'libgirepository'
  'libgmobile'
  'libgudev'
)
makedepends=(
  'gi-docgen'
  'glib2-devel'
  'gobject-introspection'
  'meson'
  'python-docutils'
  'vala'
)
checkdepends=('umockdev')
optdepends=('feedbackd-device-themes: Device-specific themes')
source=(
  https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
  fbd-sysusers.conf
)
sha256sums=('a7c74bd24e09d458ff0bc6f9a619cd13b93cba49111abc617dcfa2d30c0575b2'
            'SKIP'
            '4d46dbd3c6eaa762bea773331a4149bb83d29e7740b6ec501e708b3f8e4c1a22')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  local meson_options=(
    --libexecdir=/usr/lib/$pkgname
    -D gtk_doc=true
    -D man=true
  )
  arch-meson $pkgname-$pkgver output "${meson_options[@]}"
  ninja -C output
}

check() {
  ninja -C output test
}

package() {
  DESTDIR="$pkgdir" ninja -C output install

  install -D -m 0644 "$srcdir"/fbd-sysusers.conf "$pkgdir"/usr/lib/sysusers.d/feedbackd.conf

  sed -i 's/libexec/lib\/feedbackd/g' "$pkgdir"/usr/lib/udev/rules.d/72-feedbackd.rules
}
