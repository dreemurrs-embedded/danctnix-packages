# Maintainer: Danct12 <danct12@disroot.org>
pkgname=cellbroadcastd
pkgver=0.0.3
pkgrel=1
pkgdesc="dbus daemon that manages cell broadcast messages"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later AND LGPL-2.1-or-later')
depends=(
  'libgirepository'
  'libgmobile'
  'modemmanager'
)
makedepends=(
  'glib2-devel'
  'gobject-introspection'
  'meson'
  'vala'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('74847a8a8a54f5e7c6d695936f8603e1146b9d3e86b1a76f3683bf16def93bd0a39dcf1ffda1a5abacdf802d6a04d7c68986a2b4d32ef577a49f18056fe3f499'
        'SKIP')
validpgpkeys=('B9386554B7DD266BCB8E29A990F0C9B18A6B4A19')

build() {
  arch-meson $pkgname-$pkgver output
  meson compile -C output
}

check() {
  meson test -C output --print-errorlogs
}

package() {
  meson install -C output --skip-subprojects --destdir "$pkgdir"
}
