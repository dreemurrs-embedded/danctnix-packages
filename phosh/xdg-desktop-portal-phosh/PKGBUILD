# Maintainer: Danct12 <danct12@disroot.org>
pkgname=xdg-desktop-portal-phosh
pkgver=0.53.0
pkgrel=1
pkgdesc="Backend implementation for xdg-desktop-portal for Phosh mobile environment"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
  'fontconfig'
  'glib2'
  'gnome-desktop-4'
  'gtk4'
  'libadwaita'
  'xdg-desktop-portal'
)
makedepends=(
  'glib2-devel'
  'meson'
  'python-docutils'
  'rust'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('da106428e8f0640a8a4f9e029e47c4e2f37ee7e7ac10491b3eee81a2dae5d122b52870d5bcfa1db236ea2c7c8d16aedf280113a434e2dd29460c1f9373107251'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
