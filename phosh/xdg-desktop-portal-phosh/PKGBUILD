# Maintainer: Danct12 <danct12@disroot.org>
pkgname=xdg-desktop-portal-phosh
pkgver=0.51.0
pkgrel=1
pkgdesc="Backend implementation for xdg-desktop-portal for Phosh mobile environment"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
  'fontconfig'
  'glib2'
  'gnome-desktop-4'
  'gtk4'
  'libadwaita'
  'xdg-desktop-portal'
)
makedepends=(
  'glib2-devel'
  'meson'
  'python-docutils'
  'rust'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('9adec20a66356b721b1047ca36df7584b2dd21eca6841e50ae2636c8d6db8135f108315ccdb85d79045ef1770014999e72f20c194d39e5fb2726e90deae4d251'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
