# Maintainer: Danct12 <danct12@disroot.org>
pkgname=xdg-desktop-portal-phosh
pkgver=0.49.0
pkgrel=1
pkgdesc="Backend implementation for xdg-desktop-portal for Phosh mobile environment"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
  'fontconfig'
  'glib2'
  'gnome-desktop-4'
  'gtk4'
  'libadwaita'
  'xdg-desktop-portal'
)
makedepends=(
  'glib2-devel'
  'meson'
  'rust'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('8a485f48e195e6cff5c0e766ca063f6962a05a4dd35da896c32db197121791a58fd52b1e9867a99afbbdfc0af326b9b634c2000767facea0c55bbe2d95faa6ec'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
