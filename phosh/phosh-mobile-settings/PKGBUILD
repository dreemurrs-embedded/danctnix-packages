# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-mobile-settings
pkgver=0.53.0
pkgrel=1
pkgdesc="Phosh-specific settings app"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
    'cellbroadcastd'
    'gtk4'
    'gsound'
    'libadwaita'
    'libgmobile'
    'libyaml'
    'lm_sensors'
    'phosh'
    'wayland'
)
makedepends=(
    'git'
    'glib2-devel'
    'meson'
    'python-packaging'
    'wayland-protocols'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('293940272e7a0497b09ab8bcec1b3828e9f9f913e063df095da93c08990134acfddfe4262428387d809ab91b2cc3b2401b7c7671d310e3afe3d90b12aacbdbce'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
    arch-meson $pkgname-$pkgver output
    meson compile -C output
}

package() {
    meson install -C output --skip-subprojects --destdir "$pkgdir"
}
