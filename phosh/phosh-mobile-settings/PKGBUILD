# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-mobile-settings
pkgver=0.52.0
pkgrel=1
pkgdesc="Phosh-specific settings app"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
    'cellbroadcastd'
    'gtk4'
    'gsound'
    'libadwaita'
    'libgmobile'
    'libyaml'
    'lm_sensors'
    'phosh'
    'wayland'
)
makedepends=(
    'git'
    'glib2-devel'
    'meson'
    'python-packaging'
    'wayland-protocols'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('0576ea6360cf4116b17b088936c59af1a25df71444acf1d61e8301ffb4aa66a333bb84a97c937419a5637f04db995a36f71dbbde84fe006fa89604ace01295c1'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
    arch-meson $pkgname-$pkgver output
    meson compile -C output
}

package() {
    meson install -C output --skip-subprojects --destdir "$pkgdir"
}
