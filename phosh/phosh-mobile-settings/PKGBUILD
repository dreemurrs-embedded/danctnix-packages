# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-mobile-settings
pkgver=0.50.0
pkgrel=1
pkgdesc="Phosh-specific settings app"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
    'gtk4'
    'gsound'
    'libadwaita'
    'libgmobile'
    'libyaml'
    'lm_sensors'
    'phosh'
    'wayland'
)
makedepends=(
    'git'
    'glib2-devel'
    'meson'
    'python-packaging'
    'wayland-protocols'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('ad6bb19e21bb6d954eeb2f2fb28afc434dc9029df6e488fd40d74cd095d48585fb22395b084fe4397ac1aaa99c39bf18c2aa36a25a9a6edaefc92c0c6b6280a6'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
    arch-meson $pkgname-$pkgver output
    meson compile -C output
}

package() {
    meson install -C output --skip-subprojects --destdir "$pkgdir"
}
