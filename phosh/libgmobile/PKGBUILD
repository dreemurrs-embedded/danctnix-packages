# Contributor: Jelle van der Waa <jelle@archlinux.org>

_pkgname=gmobile
pkgname=lib${_pkgname}
pkgver=0.6.0
pkgrel=1
pkgdesc='Functions useful in mobile related, glib based projects'
arch=('x86_64' 'armv7h' 'aarch64')
url='https://gitlab.gnome.org/World/Phosh/gmobile'
license=(LGPL-2.1-or-later)
depends=(
  'glib2'
  'json-glib'
  'libgirepository'
)
makedepends=(
  'gi-docgen'
  'gobject-introspection'
  'meson'
  'python-docutils'
  'vala'
)
checkdepends=('xorg-server-xvfb')
source=(https://sources.phosh.mobi/releases/${_pkgname}/${_pkgname}-${pkgver}.tar.xz{,.asc})
sha256sums=('0786ffd2b0423cd1e5e4998ae4d602fc1f9dbad35c4312b570e0756bfa24cde3'
            'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
  local meson_options=(
    -D gtk_doc=true
    -D man=true
  )

  arch-meson $_pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
