# Maintainer: Danct12 <danct12@disroot.org>
pkgname=livi
pkgver=0.4.0
pkgrel=1
pkgdesc="GTK4 video player using GStreamer as backend"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/guidog/livi"
license=('GPL-3.0-or-later')
depends=(
    'gstreamer'
    'gst-plugins-bad'
    'gst-plugins-base'
    'gtk4'
    'libadwaita'
)
makedepends=(
    'glib2-devel'
    'meson'
    'python-packaging'
)
checkdepends=('xorg-server-xvfb')
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
    arch-meson ${pkgname}-${pkgver} output
    meson compile -C output
}

check() {
    xvfb-run meson test -C output
}

package() {
    meson install -C output --destdir "$pkgdir"
}

md5sums=('17035503aaa726f4f10ed43d8e2999ec'
         'SKIP')
